plugins {
    id "de.undercouch.download" version "4.1.2"
}

apply plugin: 'com.android.application'

project.ext.ASSET_DIR = projectDir.toString() + '/openrct2-data'

task myCopy(type: Copy) {
    from "${projectDir.toString()}/../../../data/"
    into project.ext.ASSET_DIR
}

//assemble.dependsOn myCopy
tasks.whenTaskAdded { task ->
    switch (task.name) {
       case 'assembleDebug':
       case 'assembleRelease':
       case 'mergeDebugAssets':
       case 'mergeReleaseAssets':
       case 'lintVitalAnalyzeRelease':
           task.dependsOn 'myCopy'
    }
}

apply from: 'download_openrct2_data.gradle'

android {
    compileSdkVersion 31
    ndkVersion "23.1.7779620" // Latest r23b (LTS), to be synced with CI container image
    defaultConfig {
        applicationId 'io.openrct2'
        minSdkVersion 19
        targetSdkVersion 31

        versionCode 2
        versionName '0.3.5.1'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
                targets 'openrct2', 'openrct2-ui', 'openrct2-cli'
                // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                abiFilters 'arm64-v8a'
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
        assets.srcDirs = [project.ext.ASSET_DIR]
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
            version "3.18.1"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'commons-io:commons-io:2.6'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

